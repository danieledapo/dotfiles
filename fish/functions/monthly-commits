#!/usr/bin/env fish

function monthly-commits
    if test (count $argv) -eq 0
        set input_date (date +%Y%m%d)
    else
        set input_date $argv[1]
    end

    set first (date -d $input_date '+%Y-%m-01'; or exit 1)
    set last (date -d "$first +1 month -1 day" '+%Y-%m-%d')
    set month (env LC_TIME=en_US.UTF-8 date -d "$first" '+%B')

    set hilight_days 0
    if test (date -d $input_date '+%Y-%m') = (date '+%Y-%m')
        set days (date '+%d')
        set hilight_days 1
    else
        set days (date -d $last '+%d')
    end

    set data (
        http https://github-contributions-api.now.sh/v1/d-dorazio \
        | jq -r ".contributions | map(select(.date >= \"$first\" and .date <= \"$last\")) | reverse | .[].count"
    )

    printf "\n         \033[1m$month\033[0m\n\n  "

    set total 0
    for c in $data
        set total (math "$total + $c")
    end

    set average (math -s2 "$total / $days")

    set col 0
    set row 0
    for c in $data
        if test $c -eq 0
            set color "\033[38;2;128;128;128m"
        else if test $c -eq 1
            set color "\033[38;2;0;128;0m"
        else if test $c -eq 2
            set color "\033[38;2;92;166;27m"
        else if test $c -lt 4
            set color "\033[38;2;136;222;35m"
        else
            set color "\033[38;2;173;255;47m"
        end

        set background ""
        if test $hilight_days -eq 1 -a (math "$row * 7 + $col + 1") -eq $days
            set background "\033[48;2;64;64;64m"
        end
        printf "$background$color%2d\033[m " $c

        if test $col -eq 6
            if test $row -eq 0
              printf "  total: $total"
            else if test $row -eq 1
              printf "  average: $average"
            end

            set row (math $row + 1)
            printf "\n  "
        end

        set col (math "($col + 1) % 7")
    end

    printf "\n\n"
end
